10 GOSUB 2730: ' This is the Clear Screen subroutine
20  F = 0
30 ON ERROR GOTO 2610
40 PRINT " HAEMODYNAMICS AND OXYGEN TRANSPORT STUDIES "
50 PRINT
60 PRINT "                         <V 3.5> 20-OCT-85"
70 PRINT
80 PRINT "                         <C>  P.H.V. CUMPSTON "
90 PRINT
100 PRINT "                              HALE SOFTWARE"
110 PRINT : PRINT : PRINT : PRINT
120 PRINT " PLEASE ENTER THE DATA AS REQUESTED....."
130 PRINT
140 FOR X = 1 TO 1500: NEXT X
150 GOSUB 2730: PRINT "HAEMODYNAMICS": PRINT "-------------"
160 CLEAR : DEFDBL A-Z
170 PRINT : CLEAR (1000)
180 ON ERROR GOTO 2610
190 LINE INPUT "NAME (E.G. ROBERTS,JOHN....)"; N4$
200 INPUT "MEDICAL RECORD NUMBER  "; N3$
210 INPUT "DATE (E.G. 09-04-83) "; D1$
220 LINE INPUT "TIME (E.G. 21:03 ) "; T$
230 PRINT "ENTER INOTROPES AND DOSE "
240 PRINT "(e.g. DOPAMINE 5MCG/Kg/min;DOBUTAMINE MCG/Kg/min)"
250 LINE INPUT I1$
260 PRINT : PRINT
270 PRINT "ARE THE DATA CORRECT?   <Y/N> "
280 A5$ = INKEY$
285 IF A5$ = "" THEN 280
290 IF A5$ = "Y" OR A5$ = "y" THEN GOTO 300 ELSE IF A5$ = "N" OR A5$ = "n" THEN GOTO 150 ELSE GOTO 280
300 GOSUB 2730
310 INPUT "HEIGHT (CM)       "; H1
320 INPUT "WEIGHT (KG)       "; K1
330 INPUT "HEART RATE (B/MIN)"; R0
340 PRINT : PRINT "N.B.  ALL PRESSURES ARE IN MMHG.  ": PRINT
350 INPUT "ARTERIAL B.P. - SYSTOLIC  "; P1
360 INPUT "ARTERIAL B.P. - DIASTOLIC "; P2
370 INPUT "ARTERIAL B.P. - MEAN      "; P3
380 '          ** ? INSERT CHECK ON MEAN HERE ? **
390 INPUT "PULMONARY ARTERIAL PRESSURE - MEAN "; P4
400 INPUT "PULMONARY ARTERIAL PRESSURE - WEDGE "; P5
410 INPUT "CENTRAL VENOUS PRESSURE - MEAN      "; P6
415 PRINT : PRINT : PRINT : PRINT
420 PRINT "ARE THE DATA CORRECT?   <Y/N> "
430 A5$ = INKEY$
435 IF A5$ = "" THEN 430
440 IF A5$ = "Y" OR A5$ = "y" THEN GOTO 450 ELSE IF A5$ = "N" OR A5$ = "n" THEN GOTO 300 ELSE GOTO 430
450 GOSUB 2730
460 PRINT "CARDIAC OUTPUT MEASUREMENTS..."
470 PRINT : PRINT : PRINT : PRINT
480 OPEN "O", #1, "COUTPUT.DAT"
490 INPUT "HOW MANY CARDIAC OUTPUT MEASUREMENTS ARE THERE "; N0
500 PRINT : PRINT
510 N1 = 0: N2 = 0
520 FOR X = 1 TO N0
530    PRINT "ENTER VALUE NUMBER "; X;
540    INPUT N2
550    PRINT #1, N2
560    N1 = N1 + N2
570 N2 = 0
580 NEXT X: PRINT
590 CLOSE #1
592 PRINT "ARE YOU HAPPY WITH THESE ENTRIES      <Y/N>  "
594 A5$ = INKEY$: IF A5$ = "" THEN 594
596 IF A5$ = "Y" OR A5$ = "y" THEN 600 ELSE LET A5$ = " ": GOTO 450
600 GOSUB 2730: A1$ = ""
610 PRINT "DO YOU WANT TO CHECK THE DATA SO FAR (ENTER <Y/N> "
612 A1$ = INKEY$: IF A1$ = "" THEN 612
620 IF A1$ = "N" OR A1$ = "n" THEN 630 ELSE IF A1$ = "Y" OR A1$ = "y" THEN 2770 ELSE A1$ = "": GOTO 600
630  Q1 = N1 / N0: '       *  MEAN CARDIAC OUTPUT  *
640  M1 = (K1 ^ .425) * (H1 ^ .725) * 71.84 / 10000: '  *S.A. - DUBOIS *
650  C1 = Q1 / M1: '       *  CARDIAC INDEX  *
660  R1 = 79.92 * (P3 - P6) / C1: '    * SYSTEMIC VASCULAR RESISTANCE *
670  R2 = 79.92 * (P4 - P5) / C1: '     * PULMONAR VASC. RESISTANCE *
680  S1 = 1000 * C1 / R0: '   *STROKE INDEX *
690  W1 = S1 * P3 * .0144: ' * L.V. S.W.I. *
700  W2 = S1 * P4 * .0144: ' * R.V. S.W.I. *
710  W3 = C1 * P3 * .0144: ' * LEFT CARDIAC WORK *
720  W4 = C1 * P4 * .0144: ' * RIGHT CARDIAC WORK *
730  W5 = P1 * R0: '      * RATE PRESSURE PRODUCT *
740  W6 = (P2 - P5): '      * CORONARY PERFUSION PRESSURE *
750 GOSUB 2730
755 B1$ = ""
760 PRINT "WOULD YOU LIKE TO TEST OXYGEN TRANSPORT?  <Y/N> "
762 B1$ = INKEY$
764 IF B1$ = "" THEN 762
770 IF B1$ = "Y" THEN GOTO 790 ELSE IF B1$ = "N" THEN GOTO 1530 ELSE IF B1$ = "y" THEN GOTO 790 ELSE IF B1$ = "n" THEN GOTO 1530 ELSE GOTO 750
780 '-------------------------------------------------------
790 GOSUB 2730
800 PRINT : F = 1: '    * FLAG TO NOTE O2-TRANSPORT STUDIES WANTED *
810 INPUT "WHAT IS THE HAEMOGLOBIN ( GM/DECILITRE ) "; H0
820 INPUT "CORE TEMPERATURE........................."; T1
830 INPUT "PERCENT INSPIRED OXYGEN (E.G. 60)........"; F1
840  F2 = F1 / 100: '      * TO CONVERT TO FI02 *
850  P7 = 760: ' ALT......INPUT"BAROMETRIC PRESSURE ";P7
860 PRINT : PRINT "ARTERIAL BLOOD GASES ": PRINT
870 INPUT "PH......................."; H2
880 INPUT "PCO2....................."; C2
890 INPUT "PO2......................"; O2
900 PRINT : PRINT "MIXED VENOUS BLOOD GASES ": PRINT
910 INPUT "PH........................"; H3
920 INPUT "PCO2......................"; C3
930 INPUT "PO2......................."; O3
940 PRINT : PRINT : A5$ = ""
950 PRINT "ARE THE DATA CORRECT?   <Y/N> "; A5$
960 A5$ = INKEY$: IF A5$ = "" THEN 960
965 IF A5$ = "Y" OR A5$ = "y" THEN GOTO 970 ELSE IF A5$ = "N" OR A5$ = "n" THEN GOTO 790 ELSE GOTO 960
970 GOSUB 2730
980 PRINT : PRINT : PRINT : PRINT "          <COMPUTING>"
990 '          * CALCULATE SATURATION OF HB AT CORE TEMP. *
1000 ' * FIRST CORRECT FOR EFFECTS OF SOLUBILITY OF O2,CO2,H+    * IONS AT THAT   TEMP. - SEE REFERENCE 1.*
1010  C6 = C2 * 10 ^ (.019 * (T1 - 37)): '        * CO2 TEMP. CORRECTION*
1020  C7 = C3 * 10 ^ (.019 * (T1 - 37))
1030 '????  INSERT END-CAP. CALCULATIONS HERE ????
1040  H6 = H2 - (T1 - 37) * (.0146 + .0065 * (H2 - 7.4)): ' * H2 TEMP. FIX *
1050  H7 = H3 - (T1 - 37) * (.0146 + .0065 * (H3 - 7.4))
1060 '   * NOW CORRECT O2 - USE A 'VIRTUAL' O2 IN THE SATURATION    EQUATION. *
1070   LET O4 = O2: H4 = H2: C4 = C2: GOSUB 1120: LET O6 = O4: LET S6 = S4
1080 ' THIS GIVES ARTERIAL OXYGEN SATURATION
1090  O4 = 0: H4 = 0: C4 = 0: S4 = 0: ' * ZERO THESE VARIABLES
1100  LET O4 = O3: H4 = H3: C4 = C3: GOSUB 1120: LET O7 = O4: LET S7 = S4
1110 GOTO 1350
1120 '..........................................................
1130 '          * GOSUB FOR O2 SATURATION & TEMP. CONVERSION *
1140  K0 = .243 * (4 / 100) ^ 3.88
1150  O4 = O4 * EXP((T1 - 37) * (.013 + .058 / (K0 + 1))): REM  CORRECTION FOR    OXYGEN SOLUBILITY AT THAT TEMPERATURE *
1160 ' * NOW CORRECT FOR THE EFFECTS OF TEMP AND PH ON O2-HB    AFFINITY - FIRST
1170 REM CALCULATE A 'VIRTUAL' PO2 *
1180 '    * REFERENCE 2 *
1190  K2 = (-.024 * (T1 - 37) + .4 * (H4 - 7.4) + .06 * (LOG(40) - LOG(C4)))
1200  O4 = O4 * 10 ^ (K2)
1210 '  * NOW CALCULATE SATURATION - RIUZ' ALGORITHM *
1220  L0 = 99.95 - 100 / (1 + ((O4 + 7) / 33.7) ^ 3.3)
1230  L1 = -.5 / (1 + ((O4 - 130) / 35) ^ 2)
1240  L2 = .45 / (1 + ((O4 - 68) / 12) ^ 6)
1250  L3 = -.5 / (1 + ((O4 - 35) / 3) ^ 4)
1260  L4 = -.5 / (1 + ((O4 - 15) / 4) ^ 4)
1270  L5 = .35 / (1 + ((O4 - 26) / 3) ^ 6)
1280  L6 = .2 / (1 + ((O4 - 53) / 8) ^ 4)
1290  L7 = -.4 / (1 + ((O4 - 40) / .9) ^ 4)
1300  L8 = -.2 / (1 + ((O4 - 200) / 65) ^ 8)
1310  L9 = .4 / (1 + ((O4 - 9) / 3) ^ 2)
1320  S4 = L0 + L1 + L2 + L3 + L4 + L5 + L6 + L7 + L8 + L9
1330 RETURN: '           * END OF O2 GOSUB *
1340 '----------------------------------------------------------
1350 '  * CALCULATE O2 TRANSPORT PARAMETERS *
1360  Q6 = (H0 * 1.39) * S6 / 100 + (O6 * .0031): 'ARTERIAL O2 CONTENT(ML/DL)
1370  Q7 = (H0 * 1.39) * S7 / 100 + (O7 * .0031): 'MIXED VENOUS O2 CONTENT
1380  D2 = Q6 - Q7: '* ARTERIAL/MIXED VENOUS OXYGEN CONTENT DIFFERENCE
1390  V1 = 100 * D2 / Q6: '  * O2 EXTRACTION RATIO  (%) *
1400  V2 = Q6 * 10 * C1: '   * O2 DELIVERY (ML/MIN/M[2) *
1410  V3 = C1 * D2 * 10: '   * O2 CONSUMPTION (ML/MIN/M[2) *
1420 '  * CALCULATE VALUES OF PO2 & SO2 FOR END-CAP. BLOOD *
1430  O5 = F2 * (P7 - 47) - C6 / .8: ' NB CO2 IS CORRECTED FOR TEMP. -    COULD CORRECT FOR SATURATED WATER VAPOUR AT BODY TEMP.
1440  H4 = H2: O4 = O5: C4 = C2: GOSUB 1120: O0 = O4: S0 = S4
1450  O4 = 0: S4 = 0: H4 = 0: C4 = 0
1460  G1 = O0 - O6: '     * ALVEOLAR/ARTERIAL  O2 DIFFERENCE *
1470  G2 = (O6 / O0) * 100: '  * ALVEOLAR/ARTERIAL RATIO *
1480 '  * CALCULATE END-CAP. O2 CONTENT *
1490  C5 = (H0 * 1.39) * S0 / 100 + (O0 * .0031)
1500  Q2 = (.0031 * G1 * 100) / (.0031 * G1 + D2): ' * SHUNT (%) *
1510  V4 = V3 * 6.948 * M1: '  KCAL/DAY   %  ESTIMATE OF AEROBIC CALORIC NEEDS  BASED ON - O2 CONSUMPTION(L/MIN) * 4.825KCAL/L O2 * SURFACE AREA. *
1520 '-----------------------------------------------------------
1530 ' * PRINTOUT CALCULATED PARAMETERS HERE *
1540  F6 = 0: A8$ = "": GOSUB 2730
1550 PRINT "DO YOU WANT THE OUTPUT TO GO TO ........<S>CREEN "
1552 PRINT : PRINT "                                ........<P>RINTER"
1554 PRINT : PRINT "                                ........<D>ISK"
1555 PRINT : PRINT : PRINT
1556 PRINT "        Enter the letter corresponding to your choice  "
1558 A8$ = INKEY$: IF A8$ = "" THEN 1558
1560 IF A8$ = "P" OR A8$ = "p" THEN GOSUB 3060: 'SEND DISPLAY TO THE PRINTER
1562 IF A8$ = "S" OR A8$ = "s" THEN GOSUB 1600: 'SCREEN DISPLAY OF DATA
1564 IF A8$ = "D" OR A8$ = "d" THEN GOSUB 4000: 'DISK FILE SUBROUTINE
1570 GOSUB 2730: A7$ = "": PRINT "DO YOU WANT TO REVIEW THIS DATA?  <Y/N> "
1571 A7$ = INKEY$: IF A7$ = "" THEN 1571
1572 IF A7$ = "Y" OR A7$ = "y" THEN 1520 ELSE IF A7$ = "N" OR A7$ = "n" THEN 1573 ELSE 1570
1573 GOSUB 2730: PRINT : PRINT : PRINT : PRINT
1574 A7$ = ""
1575 PRINT "DO YOU WANT TO INPUT MORE DATA...<Y/N> "
1576 A7$ = INKEY$: IF A7$ = "" THEN 1576
1577 IF A7$ = "Y" OR A7$ = "y" THEN 10 ELSE IF A7$ = "N" OR A7$ = "n" THEN 1578 ELSE 1573
1578 GOSUB 2730: PRINT : PRINT : PRINT : PRINT
1579 PRINT "NOW RETURNING TO DISK OPERATING SYSTEM"
1580 CLOSE : SYSTEM
1600 '===========================================================================
1605 ' SUBROUTINE FOR SCREEN DISPLAY
1610 GOSUB 2730: Y$ = "###"
1620 PRINT " ROYAL CANBERRA HOSPITAL I.C.U. "
1630 PRINT "--------------------------------": PRINT
1640 PRINT N4$; "             M.R.N. "; N3$
1650 PRINT "TIME  "; T$; " HOURS"
1660 PRINT "DATE   "; D1$
1670 PRINT
1680 PRINT "HAEMODYNAMICS AND OXYGEN TRANSPORT STUDIES"
1690 PRINT "------------------------------------------"
1700 PRINT "HAEMODYNAMICS - PATIENT DATA"
1710 PRINT "---------------------------- ": PRINT
1720 PRINT "INOTROPES "; I1$
1730 PRINT "HEART RATE (B/MIN).................."; : PRINT USING Y$; R0
1740 PRINT "ARTERIAL B.P. - SYSTOLIC............"; : PRINT USING Y$; P1; : PRINT " MM.HG."
1750 PRINT "ARTERIAL B.P. - DIASTOLIC..........."; : PRINT USING Y$; P2; : PRINT " MM.HG."
1760 PRINT "ARTERIAL B.P. - MEAN................"; : PRINT USING Y$; P3; : PRINT " MM.HG."
1770 PRINT "PULMONARY ARTERIAL PRESSURE - MEAN.."; : PRINT USING Y$; P4; : PRINT " MM.HG."
1780 PRINT "PULMONARY ARTERIAL PRESSURE - WEDGE."; : PRINT USING Y$; P5; : PRINT " MM.HG."
1790 PRINT "CENTRAL VENOUS PRESSURE - MEAN......"; : PRINT USING Y$; P6; : PRINT " MM.HG."
1800 PRINT "HEIGHT (CM)...."; : PRINT USING Y$; H1; : PRINT "    WEIGHT (KG)..."; : PRINT USING Y$; K1
1810 U$ = "####.##": '  * USED TO PRINT TO 2 DECIMAL PLACES *
1820 W$ = "#######"
1830 PRINT
1840 Z1$ = ""
1850 PRINT "Tap Return When Ready for next Page"
1855 Z1$ = INKEY$: IF Z1$ = "" THEN 1855 ELSE 1870
1870 GOSUB 2730
1890 PRINT "RESULTS"
1900 PRINT "-------": PRINT
1910  PRINT "SURFACE AREA................."; : PRINT USING U$; M1; : PRINT " SQ. METRES"
1920 OPEN "I", #1, "COUTPUT.DAT"
1930 PRINT "CARDIAC OUTPUTS MEASURED.....  ";
1940 FOR X = 1 TO N0
1950  INPUT #1, N2
1960  PRINT N2; : PRINT ";";
1970 NEXT X
1980 PRINT "L/MIN."
1990 CLOSE #1
2000 PRINT "CARDIAC OUTPUT..............."; : PRINT USING U$; Q1; : PRINT " L/MIN."
2010 PRINT "CARDIAC INDEX................"; : PRINT USING U$; C1; : PRINT " (N = 2.8-3.6)"
2020 PRINT "SYSTEMIC VASCULAR RESISTANCE."; : PRINT USING W$; INT(R1); : PRINT " (N = 1760-2600 DYNE.CM/SEC)"
2030 PRINT "PULMONARY VASCULAR RESISTANCE"; : PRINT USING W$; INT(R2); : PRINT " (N =  45 - 225 DYNE.CM/SEC)"
2040 PRINT "STROKE INDEX................."; : PRINT USING W$; INT(S1); : PRINT " (N = 30-50 ML/M^2)"
2050 PRINT "L.V.S.W.I...................."; : PRINT USING W$; INT(W1); : PRINT " (N = 44-68 G.M./BEAT.M^2)"
2060 PRINT "R.V.S.W.I...................."; : PRINT USING W$; INT(W2); : PRINT " (N =  4-8  G.M./BEAT.M^2)"
2070 PRINT "LEFT CARDIAC WORK............"; : PRINT USING U$; W3; : PRINT "(N = 3.0-4.6; D> 5.1";
2080 PRINT "KG.M/M^2)"
2090 PRINT "RIGHT CARDIAC WORK..........."; : PRINT USING U$; W4; : PRINT "(N = 0.4-0.6 KG.M./M^2)"
2100 PRINT "RATE - PRESSURE PRODUCT......"; : PRINT USING W$; INT(W5)
2110 PRINT "CORONARY PERFUSION PRESSURE.."; : PRINT USING W$; INT(W6); : PRINT " MM.HG."
2130 PRINT : PRINT : PRINT : PRINT : PRINT : PRINT
2140 PRINT "Tap Return When Ready for next Page"
2150 Z1$ = INKEY$: IF Z1$ = "" THEN 2150 ELSE 2160
2160 GOSUB 2730
2170 IF B1$ = "N" OR B1$ = "n" THEN 2460
2190 REM     * NOW PRINT O2-TRANSPORT STUDIES *
2200 V$ = "####.##"
2210 V1$ = "####.#"
2220 W$ = "#######"
2230 PRINT "OXYGEN TRANSPORT STUDIES - PATIENT DATA"
2240 PRINT "---------------------------------------": PRINT
2250 PRINT "HAEMOGLOBIN ( GM/dL ).."; : PRINT USING V1$; H0
2260 PRINT "CORE TEMP.............."; : PRINT USING V1$; T1;
2270 PRINT " Deg.C.     ";
2280 PRINT "INSPIRED OXYGEN........"; : PRINT USING Y$; F1;
2290 PRINT " %": PRINT
2300 PRINT "ARTERIAL BLOOD GASES           MIXED VENOUS BLOOD GASES "
2310 PRINT "--------------------           ------------------------ ": PRINT
2320 PRINT "PH.................."; : PRINT USING V$; H2; : PRINT "    PH.................."; : PRINT USING V$; H3
2330 PRINT "PCO2................"; : PRINT USING W$; C2; : PRINT "    PCO2................"; : PRINT USING W$; C3
2340 PRINT "PO2................."; : PRINT USING W$; O2; : PRINT "    PO2................."; : PRINT USING W$; O3
2350 PRINT : PRINT : PRINT
2352 PRINT "Tap Return When Ready for next Page"
2354 Z1$ = INKEY$: IF Z1$ = "" THEN 2354 ELSE 2356
2356 GOSUB 2730
2360 PRINT "RESULTS"
2370 PRINT "-------": PRINT
2380 PRINT "A-V DO2................"; : PRINT USING U$; D2; : PRINT " (N = 4-5.5 ML/DL)"
2390 PRINT "O2 DELIVERY............"; : PRINT USING W$; INT(V2); : PRINT " (N = 520-720 ML/MIN.M^2)"
2400 PRINT "O2 CONSUMPTION........."; : PRINT USING W$; INT(V3); : PRINT " (N = 100-180 ML/MIN.M^2)"
2410 PRINT "O2 EXTRACTION RATIO...."; : PRINT USING W$; INT(V1); : PRINT " (N = 22-30 %)"
2420 PRINT "CALORIC NEEDS..........  "; INT(V4); " CALORIES PER DAY "
2430 PRINT "ALVEOLAR-ARTERIAL DO2.."; : PRINT USING W$; INT(G1); : PRINT " (N = 0-100:FIO2 =1.0;0-20:FIO2 = 0.21)"
2440 PRINT "ARTERIAL-ALVEOLAR RATIO"; : PRINT USING W$; INT(G2); : PRINT " %"
2450 PRINT "PULMONARY SHUNT........"; : PRINT USING W$; INT(Q2); : PRINT " %"
2451 PRINT : PRINT : PRINT
2452 PRINT "Tap Return When Ready for next Page"
2454 Z1$ = INKEY$: IF Z1$ = "" THEN 2454 ELSE 2456
2456 GOSUB 2730
2460 RETURN
2600 '=========================================================
2605 ' ERROR MESSAGE AND START AGAIN
2610 GOSUB 2730
2620 PRINT
2630 PRINT "      YOU WILL BE TAKEN OUT TO THE LOGIN REQUEST AND WILL HAVE TO"
2640 PRINT
2650 PRINT "      START ALL OVER AGAIN"
2660 PRINT
2670 PRINT "      PLEASE DO NOT TRY THAT AGAIN!"
2680 PRINT : PRINT : PRINT : PRINT : PRINT : PRINT : PRINT : PRINT : PRINT : PRINT
2690 FOR Z = 1 TO 1500: NEXT Z
2700 RESUME 10
2710 END
2720 '=========================================================
2730 REM * CLEAR SCREEN GOSUB - CHANGE FOR DIFFERING MACHINES *
2740 CLS
2750 RETURN
2760 '=========================================================
2770 REM    * ENTRY ERRORS GOSUB*
2780 GOSUB 2730
2790 PRINT "NAME: "; N4$
2800 PRINT "MEDICAL RECORD NUMBER: "; N3$
2810 PRINT "TIME: "; T$; " HOURS"; "   DATE: "; D1$
2820 PRINT "INOTROPES: "; I1$
2830 PRINT
2840 PRINT "HEIGHT: "; H1; " cm."; "     WEIGHT: "; K1; " Kg."
2850 PRINT "HEART RATE: "; R0; " B.P.M."
2860 PRINT "ARTERIAL B.P.: ....SYSTOLIC: "; P1; " mmHg."
2870 PRINT "               ...DIASTOLIC: "; P2; " mmHg."
2880 PRINT "               ........MEAN: "; P3; " mmHg."
2890 PRINT "P. A. PRESSURES: ......MEAN: "; P4; " mmHg."
2900 PRINT "                 .....WEDGE: "; P5; " mmHg."
2910 PRINT "C.V.P.: ...............MEAN: "; P6; " mmHg."
2920 PRINT
2930 PRINT "CARDIAC OUTPUTS MEASURED: ";
2940 OPEN "I", #1, "COUTPUT.DAT"
2950 FOR X = 1 TO N0
2960 INPUT #1, N2
2970 PRINT N2; : PRINT ";";
2980 NEXT X
2990 PRINT "L/min.": PRINT
3000 CLOSE
3010 PRINT : PRINT : PRINT : PRINT
3020 PRINT "DO YOU WANT TO MAKE ANY CHANGES? ...<Y/N> "
3030 A$ = INKEY$: IF A$ = "" THEN 3030 ELSE IF A$ = "N" THEN 3050 ELSE IF A$ = "n" THEN 3050 ELSE IF A$ = "Y" THEN 3040 ELSE IF A$ = "y" THEN 3040: GOTO 2770
3040 GOTO 140: '  * MAKE CHANGES HERE *
3050 GOTO 630
3060 '-----------------------------------------------------------
3070 'GOSUB FOR PRINTER
3090 'MULTITASKING MAY REQUIRE A ^G TO REQUEST ACCESS TO PRINTER
3100 GOSUB 2730
3105 INPUT "READY PRINTER AND PRESS <RETURN> "; A9$
3110 PRINT : PRINT : PRINT : PRINT
3120 PRINT "All output will now go to the printer......"
3130 Y$ = "###"
3140 LPRINT " ROYAL CANBERRA HOSPITAL I.C.U. "
3150 LPRINT "--------------------------------": LPRINT
3160 LPRINT N4$; "             M.R.N. "; N3$
3170 LPRINT "TIME  "; T$; " HOURS"
3180 LPRINT "DATE   "; D1$
3190 LPRINT
3200 LPRINT "HAEMODYNAMICS AND OXYGEN TRANSPORT STUDIES"
3210 LPRINT "------------------------------------------": LPRINT
3220 LPRINT "HAEMODYNAMICS - PATIENT DATA"
3230 LPRINT "---------------------------- ": LPRINT
3240 LPRINT "INOTROPES "; I1$
3250 LPRINT "HEART RATE (B/MIN).................."; : LPRINT USING Y$; R0
3260 LPRINT "ARTERIAL B.P. - SYSTOLIC............"; : LPRINT USING Y$; P1; : LPRINT " MM.HG."
3270 LPRINT "ARTERIAL B.P. - DIASTOLIC..........."; : LPRINT USING Y$; P2; : LPRINT " MM.HG."
3280 LPRINT "ARTERIAL B.P. - MEAN................"; : LPRINT USING Y$; P3; : LPRINT " MM.HG."
3290 LPRINT "PULMONARY ARTERIAL PRESSURE - MEAN.."; : LPRINT USING Y$; P4; : LPRINT " MM.HG."
3300 LPRINT "PULMONARY ARTERIAL PRESSURE - WEDGE."; : LPRINT USING Y$; P5; : LPRINT " MM.HG."
3310 LPRINT "CENTRAL VENOUS PRESSURE - MEAN......"; : LPRINT USING Y$; P6; : LPRINT " MM.HG."
3320 LPRINT "HEIGHT (CM)...."; : LPRINT USING Y$; H1; : LPRINT "    WEIGHT (KG)..."; : LPRINT USING Y$; K1
3330 U$ = "####.##": '  * USED TO LPRINT TO 2 DECIMAL PLACES *
3340 W$ = "#######"
3350 LPRINT
3360 LPRINT "RESULTS"
3370 LPRINT "-------": LPRINT
3380 LPRINT "SURFACE AREA................."; : LPRINT USING U$; M1; : LPRINT " SQ. METRES"
3390 OPEN "I", #1, "COUTPUT.DAT"
3400 LPRINT "CARDIAC OUTPUTS MEASURED.....  ";
3410 FOR X = 1 TO N0
3420 INPUT #1, N2
3430 LPRINT N2; : LPRINT ";";
3440 NEXT X
3450 LPRINT "L/MIN."
3460 CLOSE
3470 LPRINT "CARDIAC OUTPUT..............."; : LPRINT USING U$; Q1; : LPRINT " L/MIN."
3480 LPRINT "CARDIAC INDEX................"; : LPRINT USING U$; C1; : LPRINT " (N = 2.8-3.6)"
3490 LPRINT "SYSTEMIC VASCULAR RESISTANCE."; : LPRINT USING W$; INT(R1); : LPRINT " (N = 1760-2600 DYNE.CM/SEC)"
3500 LPRINT "PULMONARY VASCULAR RESISTANCE"; : LPRINT USING W$; INT(R2); : LPRINT " (N =  45 - 225 DYNE.CM/SEC)"
3510 LPRINT "STROKE INDEX................."; : LPRINT USING W$; INT(S1); : LPRINT " (N = 30-50 ML/M^2)"
3520 LPRINT "L.V.S.W.I...................."; : LPRINT USING W$; INT(W1); : LPRINT " (N = 44-68 G.M./BEAT.M^2)"
3530 LPRINT "R.V.S.W.I...................."; : LPRINT USING W$; INT(W2); : LPRINT " (N =  4-8  G.M./BEAT.M^2)"
3540 LPRINT "LEFT CARDIAC WORK............"; : LPRINT USING U$; W3; : LPRINT "(N = 3.0-4.6; D> 5.1";
3550 LPRINT "KG.M/M^2)"
3560 LPRINT "RIGHT CARDIAC WORK..........."; : LPRINT USING U$; W4; : LPRINT "(N = 0.4-0.6 KG.M./M^2)"
3570 LPRINT "RATE - PRESSURE PRODUCT......"; : LPRINT USING W$; INT(W5)
3580 LPRINT "CORONARY PERFUSION PRESSURE.."; : LPRINT USING W$; INT(W6); : LPRINT " MM.HG."
3590 LPRINT : LPRINT
3600 IF B1$ = "N" OR B1$ = "n" THEN 3880
3610 REM     * NOW LPRINT O2-TRANSPORT STUDIES *
3620 V$ = "####.##"
3630 V1$ = "####.#"
3640 W$ = "#######"
3650 LPRINT "OXYGEN TRANSPORT STUDIES - PATIENT DATA"
3660 LPRINT "---------------------------------------": LPRINT
3670 LPRINT "HAEMOGLOBIN ( GM/dL ).."; : LPRINT USING V1$; H0
3680 LPRINT "CORE TEMP.............."; : LPRINT USING V1$; T1;
3690 LPRINT " Deg.C.       ";
3700 LPRINT "INSPIRED OXYGEN........"; : LPRINT USING Y$; F1;
3710 LPRINT " %": LPRINT
3720 LPRINT "ARTERIAL BLOOD GASES           MIXED VENOUS BLOOD GASES "
3730 LPRINT "--------------------           ------------------------ ": LPRINT
3740 LPRINT "PH.................."; : LPRINT USING V$; H2; : LPRINT "    PH.................."; : LPRINT USING V$; H3
3750 LPRINT "PaCO2..............."; : LPRINT USING W$; C2; : LPRINT "    PaCO2..............."; : LPRINT USING W$; C3
3760 LPRINT "PaO2................"; : LPRINT USING W$; O2; : LPRINT "    PaO2................"; : LPRINT USING W$; O3
3770 LPRINT
3780 LPRINT "RESULTS"
3790 LPRINT "-------": LPRINT
3800 LPRINT "A-V DO2................"; : LPRINT USING U$; D2; : LPRINT " (N = 4-5.5 ML/DL)"
3810 LPRINT "O2 DELIVERY............"; : LPRINT USING W$; INT(V2); : LPRINT " (N = 520-720 ML/MIN.M^2)"
3820 LPRINT "O2 CONSUMPTION........."; : LPRINT USING W$; INT(V3); : LPRINT " (N = 100-180 ML/MIN.M^2)"
3830 LPRINT "O2 EXTRACTION RATIO...."; : LPRINT USING W$; INT(V1); : LPRINT " (N = 22-30 %)"
3840 LPRINT "CALORIC NEEDS..........    "; INT(V4); " CALORIES PER DAY "
3850 LPRINT "ALVEOLAR-ARTERIAL DO2.."; : LPRINT USING W$; INT(G1); : LPRINT " (N = 0-100:FIO2 =1.0;0-20:FIO2 = 0.21)"
3860 LPRINT "ARTERIAL-ALVEOLAR RATIO"; : LPRINT USING W$; INT(G2); : LPRINT " %"
3870 LPRINT "PULMONARY SHUNT........"; : LPRINT USING W$; INT(Q2); : LPRINT " %"
3880 RETURN
4000 '============================================================================
4010 GOSUB 2730: PRINT : PRINT : PRINT
4020 PRINT "NOW PROCESSING RECORDS......."
4030 RECNUM = 1: PRINT : PRINT "         Please be patient."
4070 LSET RECNUM$ = MKI$(RECNUM): ' SET UP VARIABLES IN CORRECT FORM FOR WRITING
4080 LSET RANFLD1$ = N4$: 'NAME
4090 LSET RANFLD2$ = N3$: 'M.R.N.
4100 LSET RANFLD3$ = T$: ' TIME
4110 LSET RANFLD4$ = D1$: ' DATE
4120 LSET RANFLD5$ = I1$: 'INOTROPES
4130 LSET RANFLD6$ = MKI$(R0): 'HEART RATE
4135   LSET RANFLD7$ = MKI$(P1): ' B.P. - SYSTOLIC- MM.HG.
4140   LSET RANFLD8$ = MKI$(P2): 'ARTERIAL B.P. - DIASTOLIC-MM.HG.
4145   LSET RANFLD9$ = MKI$(P3): 'B.P. - MEAN-MM.HG.
4150   LSET RANFLD10$ = MKI$(P4): 'PULMONARY ARTERIAL PRESSURE - MEAN
4155   LSET RANFLD11$ = MKI$(P5): 'PULMONARY ARTERIAL PRESSURE - WEDGE.
4160   LSET RANFLD12$ = MKI$(P6): 'CENTRAL VENOUS PRESSURE - MEAN
4165   LSET RANFLD13$ = MKS$(H1): 'HEIGHT (CM)
4170   LSET RANFLD14$ = MKS$(K1): 'WEIGHT (KG)
4175   LSET RANFLD15$ = MKS$(M1): 'SURFACE AREA-SQ. METRES
4180   LSET RANFLD16$ = MKS$(Q1): 'AVERAGE CARDIAC OUTPUT L/MIN.
4190   LSET RANFLD17$ = MKS$(C1): 'CARDIAC INDEX
4200   LSET RANFLD18$ = MKI$(INT(R1)): 'SYSTEMIC VASCULAR RESISTANCE
4210   LSET RANFLD19$ = MKI$(INT(R2)): 'PULMONARY VASCULAR RESISTANCE
4220   LSET RANFLD20$ = MKI$(INT(S1)): 'STROKE INDEX
4230   LSET RANFLD21$ = MKI$(INT(W1)): 'L.V.S.W.I
4240   LSET RANFLD22$ = MKI$(INT(W2)): 'R.V.S.W.I
4250   LSET RANFLD23$ = MKS$(W3): 'LEFT CARDIAC WORK
4260   LSET RANFLD24$ = MKS$(W4): 'RIGHT CARDIAC WORK
4270   LSET RANFLD25$ = MKI$(INT(W5)): 'RATE - PRESSURE PRODUCT
4280   LSET RANFLD26$ = MKI$(INT(W6)): 'CORONARY PERFUSION PRESSURE
4290   LSET RANFLD27$ = MKS$(H0): 'HAEMOGLOBIN ( GM/dL )
4300   LSET RANFLD28$ = MKS$(T1): 'CORE TEMP (deg C)
4310   LSET RANFLD29$ = MKI$(F1): 'INSPIRED OXYGEN
4320   LSET RANFLD30$ = MKS$(H2): 'ARTERIAL pH
4330   LSET RANFLD31$ = MKS$(C2): '         PCO2
4340   LSET RANFLD32$ = MKS$(O2): '         PO2
4350   LSET RANFLD33$ = MKS$(H3): 'MIXED VENOUS pH
4360   LSET RANFLD34$ = MKS$(C3): '             PCO2
4370   LSET RANFLD35$ = MKS$(O3): '             PO2
4380   LSET RANFLD36$ = MKS$(D2): 'A-V DO2
4390   LSET RANFLD37$ = MKI$(INT(V2)): 'O2 DELIVERY
4400   LSET RANFLD38$ = MKI$(INT(V3)): 'O2 CONSUMPTION
4410   LSET RANFLD39$ = MKI$(INT(V1)): 'O2 EXTRACTION RATIO
4420   LSET RANFLD40$ = MKI$(INT(V4)): 'CALORIC NEEDSINT(V4) -  CALORIES PER DAY
4430   LSET RANFLD41$ = MKI$(INT(G1)): 'ALVEOLAR-ARTERIAL DO2
4440   LSET RANFLD42$ = MKI$(INT(G2)): 'ARTERIAL-ALVEOLAR RATIO - %
4450   LSET RANFLD43$ = MKI$(INT(Q2)): ' SHUNT
4500 '===============================================================================
4540 ' OPEN RANDOM ACCESS FILE FOR WRITING
4550 PRINT : PRINT : PRINT : PRINT "RECORD NUMBER... "; RECNUM
4560   OPEN "R", 1, "C35.DAT"
4570 FIELD 1, 3 AS RECNUM$, 20 AS RANFLD1$, 10 AS RANFLD2$, 5 AS RANFLD3$, 8 AS RANFLD4$, 79 AS RANFLD5$, 3 AS RANFLD6$
4580 FIELD 1, 3 AS RANFLD7$, 3 AS RANFLD8$, 3 AS RANFLD9$, 2 AS RANFLD10$, 2 AS RANFLD11$, 2 AS RANFLD12$
4590 FIELD 1, 3 AS RANFLD13$, 3 AS RANFLD14$
4600 PUT #1, RECNUM
4610 CLOSE #1
5000 RETURN

